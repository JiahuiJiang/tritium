apply plugin: 'com.palantir.publish-jar'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'

dependencies {
    compile project(":tritium-api")
    compile project(":tritium-core")
    compile project(":tritium-metrics")
    compile project(":tritium-slf4j")

    compile "cglib:cglib-nodep:${cglibVersion}"
    compile "com.google.code.findbugs:annotations:${findbugsVersion}"
    compile "com.google.guava:guava:${guavaVersion}"
    compile "org.objenesis:objenesis:${objenesisVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    processor "org.immutables:value:${immutablesVersion}"

    testCompile project(path: ":tritium-core", configuration: "testArtifacts")
    testCompile ("com.google.guava:guava-testlib:${guavaVersion}") {
        exclude group: "com.google.truth"
        exclude group: "com.google.guava", module: "guava"
    }
    testCompile "com.google.truth:truth:${truthVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile "org.slf4j:slf4j-simple:${slf4jVersion}"
}

shadowJar {
    classifier = null
    mergeServiceFiles()

    dependencies {
        // Exclude other tritium projects
        exclude project(':tritium-api')
        exclude project(':tritium-core')
        exclude project(':tritium-metrics')
        exclude project(':tritium-slf4j')

        // Exclude Guava, dropwizard metrics, and slf4j
        exclude 'com.google.code.findbugs:annotations'
        exclude 'com.google.guava:guava'
        exclude 'org.objenesis:objenesis'
        exclude 'org.slf4j:slf4j-api'

        exclude 'com/codahale/metrics/**'
        exclude 'com/google/common/**'
        exclude 'com/google/thirdparty/publicsuffix/**'
        exclude 'edu/umd/cs/findbugs/annotations/**'
        exclude 'javax/annotation/**'
        exclude 'net/jcip/annotations/**'
        exclude 'org/slf4j/**'
    }

    relocate('net.sf.cglib', 'com.palantir.tritium.shaded.net.sf.cglib')
    relocate('org.HdrHistogram', 'com.palantir.tritium.shaded.org.HdrHistogram')
    relocate('org.mpierce', 'com.palantir.tritium.shaded.org.mpierce')
    relocate('org.objenesis', 'com.palantir.tritium.shaded.org.objenesis')

}

publishing.publications.nebula {
    artifact(shadowJar)
}
